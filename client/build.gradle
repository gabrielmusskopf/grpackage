plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'com.google.protobuf' version '0.9.4'
    id 'io.ktor.plugin' version '2.3.4'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.21'
}

group = 'br.com.gabrielgmusskopf.grpackage.client'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

kotlin {
    jvmToolchain(8)
}

application {
    mainClassName = "${group}.MainKt"
}

def grpcVersion = "1.60.0"
def grpcKotlinVersion = "1.4.1"
def protobufVersion = '3.24.0'
def protocVersion = protobufVersion

dependencies {
    implementation("io.grpc:grpc-kotlin-stub:${grpcKotlinVersion}")
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.grpc:grpc-stub:${grpcVersion}")
    implementation("io.grpc:grpc-netty-shaded:${grpcVersion}")
    implementation("io.grpc:grpc-api:${grpcVersion}")

    implementation("io.ktor:ktor-server-core-jvm")
    implementation("io.ktor:ktor-server-netty")
    implementation("io.ktor:ktor-server-content-negotiation-jvm")
    implementation("io.ktor:ktor-serialization-kotlinx-json-jvm")
    implementation("ch.qos.logback:logback-classic:1.4.14")

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:0.1.5" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/sources/proto/main/grpc'
            srcDirs 'build/generated/sources/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform()
}
